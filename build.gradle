buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

ext {
    supportLibVersion = "25.3.1"
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "nu.staldal.djdplayer"
        targetSdkVersion 22

        dependencies {
            compile "com.android.support:appcompat-v7:${supportLibVersion}"
            testCompile 'junit:junit:4.12'
        }
    }

    if (project.hasProperty("KEY_STORE")) {
        signingConfigs {
            release {
                storeFile file(KEY_STORE)
                storePassword KEY_STORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
        }
    } else {
        signingConfigs {
            release {
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            debuggable false
            signingConfig signingConfigs.release
        }
        preview {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            debuggable true
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
            debuggable true
            // signing debug
        }
    }

    productFlavors {
        mobile {
            minSdkVersion 16
        }
        tv {
            minSdkVersion 21

            dependencies {
                tvCompile "com.android.support:leanback-v17:${supportLibVersion}"
            }
        }
    }

}
